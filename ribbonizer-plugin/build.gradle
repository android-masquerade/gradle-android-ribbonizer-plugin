buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

plugins {
    id('org.jetbrains.kotlin.jvm')
    id('java-gradle-plugin')
    id('signing')
    id('maven-publish')
}

dependencies {
    compileOnly gradleApi()
    implementation localGroovy()
    implementation 'com.android.tools.build:gradle:4.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

group = 'com.shogo82148.ribbonizer'
version = '3.0.2'

publishing {
    publications {
        pluginMaven(MavenPublication) {
            artifactId = 'ribbonizer-plugin'
            pom {
                name = 'ribbonizer-plugin'
                description = 'Modifies launcher icons on debug build'
                url = 'https://github.com/shogo82148/gradle-android-ribbonizer-plugin'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'shogo82148'
                        name = 'Ichinose Shogo'
                        email = 'shogo82148@gmail.com'
                    }
                }
                scm {
                    connection = 'git@github.com:shogo82148/gradle-android-ribbonizer-plugin.git'
                    developerConnection = 'git@github.com:shogo82148/gradle-android-ribbonizer-plugin.git'
                    url = 'https://github.com/shogo82148/gradle-android-ribbonizer-plugin'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "${hasProperty('sonatypeUsername') ? sonatypeUsername : 'anonymous'}"
                password = "${hasProperty('sonatypePassword') ? sonatypePassword : 'anonymous'}"
            }
        }
    }
}

signing {
    sign publishing.publications.pluginMaven
}
